plugins {
    id 'java'
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

group 'com.walksocket'
version '0.1.2'
description 'Java NIO2 Tcp wrapper for server & client.'

compileJava {
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.7.2'
}

jacoco {
    toolVersion = "0.8.7"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
    options.memberLevel = JavadocMemberLevel.PUBLIC
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'redchest'
            from components.java
            pom {
                name = 'redchest'
                packaging = 'jar'
                description = 'Java NIO2 Tcp wrapper for server & client.'
                url = 'https://github.com/shigenobu/redchest'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'PON'
                        name = 'Shigenobu Furuta'
                        email = 'shigenobu@walksocket.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/shigenobu/redchest.git'
                    developerConnection = 'scm:git:https://github.com/shigenobu/redchest.git'
                    url = 'https://github.com/shigenobu/redchest'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            def sonatypeUsername = project.hasProperty("sonatypeUsername") ? project.property("sonatypeUsername") : ""
            def sonatypePassword = project.hasProperty("sonatypePassword") ? project.property("sonatypePassword") : ""
            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava

    required { hasProperty("signing.keyId") }
    sign configurations.archives
}
